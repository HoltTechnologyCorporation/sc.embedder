*** Settings ***

Resource  keywords.txt
Library  sc.embedder.tests.test_acceptance.Keywords
Variables  plone/app/testing/interfaces.py

Suite Setup  Start Browser and Log In
Suite Teardown  Close Browser

*** Variables ***

${PORT} =  55001
${ZOPE_URL} =  http://localhost:${PORT}
${PLONE_URL} =  ${ZOPE_URL}/plone
${BROWSER} =  Firefox

*** Test cases ***

Test Required Fields
    Click Add Embedder
    Click Button  Save
    Page Should Contain  There were some errors

Test CRUD
    Add Embedder  http://vimeo.com/17914974
    Goto Homepage
    Edit Embedder  The Backwater Gospel  Some description
    Remove Content  the-blackwater-gospel

*** Keywords ***

Goto Homepage
    Go to   ${PLONE_URL}
    Page should contain  Plone site

Start Browser and Log In
    Open Browser  ${PLONE_URL}
    Go to  ${PLONE_URL}/login_form
    Page should contain element  __ac_name
    Input text  __ac_name  ${SITE_OWNER_NAME}
    Input text  __ac_password  ${SITE_OWNER_PASSWORD}
    Click Button  Log in
    Goto Homepage

Click Add Embedder
    Open Add New Menu
    Click Link  css=a#sc-embedder
    Page Should Contain  Add Embedder

Add File
    [arguments]  ${file}

    ${PATH_TO_TESTS} =  Get path to tests
    Choose File  css=input#form-widgets-image-input  ${PATH_TO_TESTS}/${file}

Add Embedder
    [arguments]  ${url}

    Click Add Embedder
    Input Text  css=input#form-widgets-url  ${url}
    Click Button  Load
    Add File  image.jpg
    Click Button  Save
    Page Should Contain  Item created

Edit Embedder
    [arguments]  ${title}  ${description}

    Click Link  ${title}
    Click Link  link=Edit
    Input Text  css=textarea#form-widgets-IDublinCore-description.textarea-widget  ${description}
    Click Button  Apply
    Page Should Contain  Changes saved
